buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'org.springframework.boot' version '2.6.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'

    // Auto Update Versions
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id 'com.github.ben-manes.versions' version '0.42.0'

    // Auto Release
    id('net.researchgate.release') version '2.8.1'
}

allprojects {
    group = 'com.raynigon.spring-boot'
    // version = project.version
    sourceCompatibility = '1.8'
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'
    apply plugin: 'signing'
    apply plugin: 'net.researchgate.release'

    sourceCompatibility = 11
    targetCompatibility = 11
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'


    apply from: "$rootDir/gradle/scripts/versionClass.gradle"
    apply from: "$rootDir/gradle/scripts/javadoc.gradle"
    apply from: "$rootDir/gradle/scripts/test.gradle"
    apply from: "$rootDir/gradle/scripts/publishing.gradle"

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
        archiveClassifier.set('')
    }

    sourceSets {
        main {
            java.srcDirs += "build/generated/sources/version/java"
        }
    }

    dependencies {
        // Commons Lang
        implementation("org.apache.commons:commons-lang3:3.12.0")

        // Spock
        testImplementation(platform("org.spockframework:spock-bom:2.1-groovy-3.0"))
        testImplementation("org.codehaus.groovy:groovy:3.0.10")
        testImplementation("org.spockframework:spock-core")
        testImplementation("org.spockframework:spock-spring")
        testRuntimeOnly("net.bytebuddy:byte-buddy:1.12.8")

        // Lombok
        compileOnly("org.projectlombok:lombok:1.18.22")
        testImplementation("org.projectlombok:lombok:1.18.20")
        annotationProcessor("org.projectlombok:lombok:1.18.20")
    }
}

bootJar {
    enabled = false
}

apply from: "$rootDir/gradle/scripts/release.gradle"
