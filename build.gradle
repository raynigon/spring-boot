buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'

    // Auto Update Versions
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id 'com.github.ben-manes.versions' version '0.51.0'

    // Auto Release
    id('net.researchgate.release') version '3.1.0'
}

allprojects {
    group = 'com.raynigon.spring-boot'
    // version = project.version
    sourceCompatibility = '21'
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'
    apply plugin: 'signing'
    apply plugin: 'net.researchgate.release'

    sourceCompatibility = 21
    targetCompatibility = 21
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'


    apply from: "$rootDir/gradle/scripts/versionClass.gradle"
    apply from: "$rootDir/gradle/scripts/javadoc.gradle"
    apply from: "$rootDir/gradle/scripts/test.gradle"
    apply from: "$rootDir/gradle/scripts/publishing.gradle"

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
        archiveClassifier.set('')
    }

    sourceSets {
        main {
            java.srcDirs += "build/generated/sources/version/java"
        }
    }

    dependencies {
        // Commons Lang
        api("org.apache.commons:commons-lang3:3.17.0")

        // Spock
        testImplementation(platform("org.spockframework:spock-bom:2.4-M5-groovy-4.0"))
        testImplementation("org.apache.groovy:groovy:4.0.24")
        testImplementation("org.spockframework:spock-core")
        testImplementation("org.spockframework:spock-spring")
        testRuntimeOnly("net.bytebuddy:byte-buddy:1.16.1")

        // Lombok
        compileOnly("org.projectlombok:lombok:1.18.36")
        testImplementation("org.projectlombok:lombok:1.18.36")
        annotationProcessor("org.projectlombok:lombok:1.18.36")
    }
}

bootJar {
    enabled = false
}

apply from: "$rootDir/gradle/scripts/release.gradle"
